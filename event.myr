use std
use sys

use "types"
use "term"
use "termios"

pkg termdraw =
	const event	: (t : term# -> event)
	const poll	: (t : term# -> std.option(event))
	const checksize	: (t : term# -> void)
;;

const Esc = 27
var winchanges = 0
var winreported = 0

const __init__ = {
	var osa
	var sa = [
		.handler = (winch : byte#),
		.flags = sys.Sarestart,
	]
	sys.sigaction(sys.Sigwinch, &sa, &osa)
}

const winch = {;
	winchanges++
}

const event = {t
	var c

	while true
		/*
		we can get more signals here, so lets try
		to do something atomic-like.
		*/
		c = winchanges
		if c > winreported
			winreported = c
			checksize(t)
			-> `Winsz (t.x, t.y)
		;;

		match readkey(t)
		| `std.Some e:	-> e
		| `std.None:	continue
		;;
	;;
	-> `Gone
}

const poll = {t
	var c = winchanges
	if c > winreported
		winreported = c
		checksize(t)
		-> `std.Some `Winsz (t.x, t.y)
	;;

	if t.nextb == 0
		-> `std.None
	;;

	-> readkey(t)
}

const checksize = {t
	var wc, x, y

	tcwinsize(t.fd, &wc)
	x = (wc.col : int)
	y = (wc.row : int)
	resize(t, x, y)
}

const readkey = {t
	var buf : byte[4]
	var n, b
	
	b = peekb(t)
	if b == 0
		-> `std.None
	elif b >= 0x80
		match std.read(t.fd, buf[:])
		| `std.Ok len:
			-> `std.Some `Kc std.decode(buf[:len+1])
		| `std.Err e:
			std.close(t.fd)
			-> `std.Some `Gone
		;;
	else
		n = t.kesc
		while true
			if b == 0
				-> `std.Some n.ev
			;;
			match n.sub[b]
			| `std.None:
				-> `std.Some n.ev
			| `std.Some nn:	
				readb(t)
				n = nn
			;;
			b = peekb(t)
		;;
	;;
	-> `std.None
}

const peekb = {t
	if t.nextb == 0
		t.nextb = readb(t)
	;;
	-> t.nextb
}


const readb = {t
	var b, buf : byte[1]
	if t.nextb != 0
		b = t.nextb
		t.nextb = 0
		-> b
	;;
	match std.read(t.fd, buf[:])
	| `std.Ok 0:	-> 0
	| `std.Ok n:	-> buf[0]
	| `std.Err e:
		std.fatal("exiting..\n", e)
	;;
}

